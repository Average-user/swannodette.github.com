---
layout: post
title: "100,000 Processes"
description: ""
category: 
tags: []
---
{% include JB/setup %}

<style>
table {
    margin-left: 45px;
    font-family: courier;
    font-size: 8px;
    line-height: 1em !important;
    margin-bottom: 50px;
}
.group0 {
    color: #000
}
.group1 {
    color: #f00
}
.group2 {
    color: #0f0
}
.group3 {
    color: #00f
}
.group4 {
    color: #ff0
}
.group5 {
    color: #0ff
}
</style>

100,000 independent go blocks all running at the same time.

<table id="big-table" cellpadding="0" cellspacing="0"></table>

```
(ns one-hundred-thousand
  (:require
    [cljs.core.async :refer [>! <! chan timeout put!]]
    [goog.dom :as dom])
  (:require-macros
    [view-bench.macros :refer [log]]
    [cljs.core.async.macros :refer [go alt!]]))

(defn by-id [id]
  (.getElementById js/document id))

(defn set-html! [el html]
  (set! (.-innerHTML el) html))

(defn set-class! [el class]
  (set! (.-className el) class))

(defn events [el type]
  (let [out (chan)]
    (.addEventListener el type
      (fn [e] (put! out e)))
    out))

(defn now []
  (js/Date.))

(def width 100)
(def height 100)

(defn gen-ui []
  (let [arr (array)]
    (loop [y 0]
      (when (< y height)
        (.push arr  "<tr>")
        (loop [x 0]
          (when (< x width)
            (.push arr (str "<td id='cell-") (+ x (* y width)) "'>0</td>")
            (recur (inc x))))
        (.push arr "</tr>")
        (recur (inc y))))
    (set-html! (by-id "big-table") (.join arr ""))))

(gen-ui)

(def group (atom 0))

(defn render! [queue]
  (let [g (str "group" @group)]
    (doseq [[idx v] queue]
      (let [cell (by-id (str "cell-" idx))]
        (set-html! cell v)
        (set-class! cell g)))
    (swap! group (fn [g] (mod (inc g) 5)))))

(defn render-loop [rate]
  (let [in (chan 1)]
    (go (loop [refresh (timeout rate) queue []]
          (let [[v c] (alts! [refresh in])]
            (condp = c
              refresh (do (render! queue)
                        (recur (timeout rate) []))
              in (recur refresh (conj queue v))))))
    in))

(let [c (events (by-id "big-table") "click")
      render (render-loop 40)]
  (loop [y 0]
    (when (< y height)
      (loop [x 0]
        (when (< x width)
          (let [idx (+ x (* y width))]
            (go (while true
                  (<! (timeout (+ 1000 (rand-int 10000))))
                  (>! render [idx (rand-int 9)]))))
          (recur (inc x))))
      (recur (inc y)))))
```

<script type="text/javascript" src="/assets/js/csp4.js"></script>


